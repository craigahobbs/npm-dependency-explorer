# Licensed under the MIT License
# https://github.com/craigahobbs/craigahobbs.github.io/blob/main/LICENSE


# Create a new SemVer object
function semverNew(major, minor, patch, release)
    semver = objectNew( \
        'major', major, \
        'minor', minor, \
        'patch', patch, \
        'release', release, \
        'build', null \
    )
    return semver
endfunction


# Parse SemVer text to a SemVer object
function semverParse(text)
    semver = null

    # Match the SemVer text
    match = if(stringLength(text) != null, regexMatch(semverRegexSemver, text), text)
    if match != null then
        groups = objectGet(match, 'groups')

        # Parse the release text
        release = null
        releaseText = objectGet(groups, 'release')
        if releaseText != null then
            release = arrayNew()
            foreach part in stringSplit(releaseText, '.') do
                # Parse all-digits parts as integers
                part = if(regexMatch(semverRegexDigits, part) != null, numberParseInt(part), part)
                arrayPush(release, part)
            endforeach
        endif

        # Return the SemVer object
        semver = objectNew( \
            'major', numberParseInt(objectGet(groups, 'major')), \
            'minor', numberParseInt(objectGet(groups, 'minor')), \
            'patch', numberParseInt(objectGet(groups, 'patch')), \
            'release', release, \
            'build', objectGet(groups, 'build') \
        )
    endif

    return semver
endfunction


# SemVer parsing regular expressions
semverRegexStrSemver = '(?<major>0|[1-9]\\d*)\\.(?<minor>0|[1-9]\\d*)\\.(?<patch>0|[1-9]\\d*)' + \
    '(?:-(?<release>(?:0|[1-9]\\d*|\\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\\.(?:0|[1-9]\\d*|\\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?' + \
    '(?:\\+(?<build>[0-9a-zA-Z-]+(?:\\.[0-9a-zA-Z-]+)*))?'
semverRegexStrSemverNoMatch = stringReplace(stringReplace(stringReplace(stringReplace(stringReplace( \
    semverRegexStrSemver, '<major>', ':'), '<minor>', ':'), '<patch>', ':'), '<release>', ':'), '<build>', ':')
semverRegexSemver = regexNew('^' + semverRegexStrSemver + '$')
semverRegexDigits = regexNew('^\d+$')


# Stringify a SemVer object
function semverStringify(semver)
    release = objectGet(semver, 'release')
    build = objectGet(semver, 'build')
    return arrayJoin(arrayNew( \
        objectGet(semver, 'major'), '.', \
        objectGet(semver, 'minor'), '.', \
        objectGet(semver, 'patch'), \
        if(release != null, '-' + arrayJoin(release, '.'), ''), \
        if(build != null, '+' + build, '') \
    ), '')
endfunction


# SemVer sort comparison func
function semverCompare(semver, other)
    # Compare the major version
    semverMajor = objectGet(semver, 'major')
    otherMajor = objectGet(other, 'major')
    result = if(semverMajor < otherMajor, -1, if(semverMajor == otherMajor, 0, 1))

    # Compare the minor version
    if !result then
        semverMinor = objectGet(semver, 'minor')
        otherMinor = objectGet(other, 'minor')
        result = if(semverMinor < otherMinor, -1, if(semverMinor == otherMinor, 0, 1))
    endif

    # Compare the patch version
    if !result then
        semverPatch = objectGet(semver, 'patch')
        otherPatch = objectGet(other, 'patch')
        result = if(semverPatch < otherPatch, -1, if(semverPatch == otherPatch, 0, 1))
    endif

    # Compare the release
    if !result then
        semverRelease = objectGet(semver, 'release')
        otherRelease = objectGet(other, 'release')
        result = if(semverRelease != null, if(otherRelease != null, 0, -1), if(otherRelease != null, 1, 0))
    endif

    # Compare the release parts
    if !result then
        semverReleaseLength = arrayLength(semverRelease)
        otherReleaseLength = arrayLength(otherRelease)
        releaseLength = mathMax(semverReleaseLength, otherReleaseLength)
        ixPart = 0
        while ixPart < releaseLength do
            semverReleasePart = arrayGet(semverRelease, ixPart)
            otherReleasePart = arrayGet(otherRelease, ixPart)

            # Numeric parts are less-than non-numeric parts
            semverPartNumeric = (stringLength(semverReleasePart) == null)
            otherPartNumeric = (stringLength(otherReleasePart) == null)
            result = if(semverPartNumeric && !otherPartNumeric, -1, if(!semverPartNumeric && otherPartNumeric, 1, 0))
            if result then
                break
            endif

            # Compare the parts
            result = if(semverReleasePart < otherReleasePart, -1, if(semverReleasePart == otherReleasePart, 0, 1))
            if result then
                break
            endif

            ixPart = ixPart + 1
        endwhile
    endif

    # Compare the release parts lengths
    if !result then
        result = if(semverReleaseLength < otherReleaseLength, -1, if(semverReleaseLength == otherReleaseLength, 0, 1))
    endif

    # Compare the build
    if !result then
        semverBuild = objectGet(semver, 'build')
        otherBuild = objectGet(other, 'build')
        result = if(semverBuild != null, if(otherBuild != null, 0, 1), if(otherBuild != null, -1, 0))
        if !result then
            result = if(semverBuild < otherBuild, -1, if(semverBuild == otherBuild, 0, 1))
        endif
    endif

    return result
endfunction


# SemVer reverse-sort comparison func
function semverCompareReversed(semver, other)
    return -semverCompare(semver, other)
endfunction


# Parse an array of SemVer text to an array of SemVer objects
function semverVersions(versions)
    semvers = arrayNew()
    foreach version in versions do
        semver = semverParse(version)
        if semver != null then
            objectSet(semver, 'semver', objectCopy(semver))
            arrayPush(semvers, semver)
        else then
            debugLog('semver: Unrecognized SemVer "' + version + '"')
        endif
    endforeach
    return arraySort(semvers, semverCompareReversed)
endfunction


# Match a SemVer range
function semverMatch(semvers, range)
    filtered = null

    # Carrot range
    matchCarrot = regexMatch(semverRangeRegexCarrot, range)
    if matchCarrot != null then
        groups = objectGet(matchCarrot, 'groups')
        major = numberParseInt(objectGet(groups, 'major'))
        minor = numberParseInt(objectGet(groups, 'minor'))
        patch = numberParseInt(objectGet(groups, 'patch'))
        lower = semverParse(matchCarrot)
        upper = semverNew( \
            if(major >= 1, major + 1, major), \
            if(major >= 1, 0, if(minor >= 1, minor + 1, minor)), \
            if(major >= 1, 0, if(minor >= 1, 0, patch + 1)), \
            arrayNew(0) \
        )
        filtered = dataFilter( \
            semvers, \
            'semverCompare(semver, lower) >= 0 && semverCompare(semver, upper) < 0', \
            objectNew('lower', lower, 'upper', upper) \
        )
        filtered = dataFilter( \
            filtered, \
            'release == null || (major == lowerMajor && minor == lowerMinor && patch == lowerPatch)', \
            objectNew('lower', lower, 'upper', upper, 'lowerMajor', major, 'lowerMinor', minor, 'lowerPatch', patch) \
        )
        return if(arrayLength(filtered), semverStringify(arrayGet(filtered, 0)))
    endif

    # Hypen Range
    matchHyphen = regexMatch(semverRangeRegexHyphen, range)
    if matchHyphen != null then
        groups = objectGet(matchHyphen, 'groups')
        lower = semverNew( \
            numberParseInt(objectGet(groups, 'major')), \
            if(objectGet(groups, 'minor'), numberParseInt(objectGet(groups, 'minor')), 0), \
            if(objectGet(groups, 'patch'), numberParseInt(objectGet(groups, 'patch')), 0) \
        )
        upper = semverNew( \
            numberParseInt(objectGet(groups, 'major2')), \
            if(objectGet(groups, 'minor2'), numberParseInt(objectGet(groups, 'minor2')), 0), \
            if(objectGet(groups, 'patch2'), numberParseInt(objectGet(groups, 'patch2')), 0) \
        )
        filtered = dataFilter( \
            semvers, \
            'semverCompare(semver, lower) >= 0 && semverCompare(semver, upper) < 0', \
            objectNew('lower', lower, 'upper', upper) \
        )
        return if(arrayLength(filtered), semverStringify(arrayGet(filtered, 0)))
    endif

    # X-Range
    matchX0 = regexMatch(semverRangeRegexX0, range)
    if matchX0 != null then
        return if(arrayLength(semvers), semverStringify(arrayGet(semvers, 0)))
    endif
    matchX1 = regexMatch(semverRangeRegexX1, range)
    if matchX1 != null then
        groups = objectGet(matchX1, 'groups')
        major = numberParseInt(objectGet(groups, 'major'))
        lower = semverNew(major, 0, 0)
        upper = semverNew(major + 1, 0, 0)
        filtered = dataFilter( \
            semvers, \
            'semverCompare(semver, lower) >= 0 && semverCompare(semver, upper) < 0', \
            objectNew('lower', lower, 'upper', upper) \
        )
        return if(arrayLength(filtered), semverStringify(arrayGet(filtered, 0)))
    endif
    matchX2 = regexMatch(semverRangeRegexX2, range)
    if matchX2 != null then
        groups = objectGet(matchX2, 'groups')
        major = numberParseInt(objectGet(groups, 'major'))
        minor = numberParseInt(objectGet(groups, 'minor'))
        lower = semverNew(major, minor, 0)
        upper = semverNew(major, minor + 1, 0)
        filtered = dataFilter( \
            semvers, \
            'semverCompare(semver, lower) >= 0 && semverCompare(semver, upper) < 0', \
            objectNew('lower', lower, 'upper', upper) \
        )
        return if(arrayLength(filtered), semverStringify(arrayGet(filtered, 0)))
    endif

    # Tilde range (major-minor)
    matchTilde2 = regexMatch(semverRangeRegexTilde2, range)
    if matchTilde2 != null then
        groups = objectGet(matchTilde2, 'groups')
        major = numberParseInt(objectGet(groups, 'major'))
        minor = numberParseInt(objectGet(groups, 'minor'))
        lower = semverNew(major, minor, 0)
        upper = semverNew(major, minor + 1, 0, arrayNew(0))
        filtered = dataFilter( \
            semvers, \
            'semverCompare(semver, lower) >= 0 && semverCompare(semver, upper) < 0', \
            objectNew('lower', lower, 'upper', upper) \
        )
        return if(arrayLength(filtered), semverStringify(arrayGet(filtered, 0)))
    endif

    return null
endfunction


# SemVer range regular expressions
semverRangeRegexCarrot = regexNew('^\\^' + semverRegexStrSemver + '$')
semverRangeRegexHyphen = regexNew( \
    '^(?<major>0|[1-9]\\d*)(?:\\.(?<minor>0|[1-9]\\d*))?(?:\\.(?<patch>0|[1-9]\\d*))?\\s*-\\s*' + \
    '(?<major2>0|[1-9]\\d*)(?:\\.(?<minor2>0|[1-9]\\d*))?(?:\\.(?<patch2>0|[1-9]\\d*))?$' \
)
semverRangeRegexX0 = regexNew('^\\*$')
semverRangeRegexX1 = regexNew('^(?<major>0|[1-9]\\d*)(?:\\.x)?(?:\\.x)?$')
semverRangeRegexX2 = regexNew('^(?<major>0|[1-9]\\d*)(?:\\.(?<minor>0|[1-9]\\d*))?(?:\\.x)?$')
semverRangeRegexTilde2 = regexNew('^~(?<major>0|[1-9]\\d*)\\.(?<minor>0|[1-9]\\d*)')
